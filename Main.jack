class Main {
  static String secret;
  static RandNumGen RNG;
  static int GUESS_WRONG, GUESS_EXACT, GUESS_IN_WORD;

  function void main() {
    var int seed;

    //initialize statics
    let GUESS_WRONG = 0;
    let GUESS_EXACT = 1;
    let GUESS_IN_WORD = 2;
    do Dictionary.init();
    do Letter.init();

    let seed = RandNumGen.genseed();
    let RNG = RandNumGen.new(seed);

    //do Tile.drawGrid();
    //do Main.drawFirstRow();
    //do Main.drawAlphabet();

    do Main.game();

    return;
  }

  function void game() {
    var String guess;
    var int round, n;
    var boolean won;

    let n = RNG.rand();
    let secret = Dictionary.word(Util.mod(n, Dictionary.size()));

    //do Main.printSecret(secret); //for debugging

    let round = 0;
    while(~won & (round < 6)) {
        let guess = Main.readWord(round);
        let won = Main.checkGuess(round, guess);
        let round = round + 1;
    }

    return;
  }

  function void printSecret(String secret) {
    do Output.moveCursor(22,0);
    do Output.printString(secret);
    do Output.moveCursor(0,0);
    return;
  }


  
  function String readWord(int round) {
    var int i;
    var char c;
    var String word;
    let word = String.new(5);
   
    do Tile.drawDottedTile(round, 5);
    do Tile.drawDottedTile(round, 6);
    do Tile.drawDottedTile(round, 7);
    do Tile.drawDottedTile(round, 8);
    do Tile.drawDottedTile(round, 9);

    let i = 0;
    while (i < 5) {
      let c = 0; // c is invalid at the start.
      while (~Main.isLetter(c)) {
        let c = Keyboard.readChar();
        do Output.backSpace();
      }
      do word.appendChar(c);
      do Tile.drawLetter(c, round, 5+i);
      let i = i + 1;
    }
    return word;
  }

  function boolean checkGuess(int round, String guess) {
    var int i, result;
    var boolean isexact;

    let isexact = true;

    let i = 0;
    while (i < 5) {
        let result = Main.checkChar(guess, i);
        if(result = GUESS_EXACT) {
          do Tile.drawSolidLetter(guess.charAt(i), round, 5+i);
        } else{if (result = GUESS_IN_WORD) { // ugh.
          do Tile.drawGrayLetter(guess.charAt(i), round, 5+i);
          let isexact = false;
        } else {
          do Tile.drawOutlinedLetter(guess.charAt(i), round, 5+i);
          let isexact = false;
        }}
        let i = i + 1;
    }

    return isexact;
  }

  function int checkChar(String guess, int i) {
    var char c;
    var int j;

    let c = guess.charAt(i);
    if (c = secret.charAt(i)) {
      return GUESS_EXACT;
    }

    let j = 0;
    while (j < 5) {
      if (c = secret.charAt(j)) {
        return GUESS_IN_WORD;
      }
      let j = j + 1;
    }

    return GUESS_WRONG;
  }

  function boolean isLetter(char c) {
    return ((c > 64) & (c < 91)) | (c = 59);
  }

  function void drawFirstRow() {
    do Tile.drawSolidLetter(83, 0, 5);
    do Tile.drawOutlinedLetter(72, 0, 6);
    do Tile.drawOutlinedLetter(73, 0, 7);
    do Tile.drawSolidLetter(82, 0, 8);
    do Tile.drawGrayLetter(69, 0, 9);
    
    return;
  }

  function void drawAlphabet() {
    var int i;
    let i = 0;
    do Screen.setColor(true);
    do Screen.drawRectangle(0, 224, 511, 255);
    while (i < 27) {
      do Letter.draw(i + 65, i + 6400 + 2, true);
      do Letter.draw(i + 65, i + 7424 + 2, false);
      let i = i + 1;
    }
    return;
  }
}
