class Main {
  static String secret;
  static RandNumGen RNG;
  static int GUESS_WRONG, GUESS_EXACT, GUESS_IN_WORD;

  function void main() {
    var int seed;

    //initialize statics
    let GUESS_WRONG = 0;
    let GUESS_EXACT = 1;
    let GUESS_IN_WORD = 2;
    do Dictionary.init();
    do Letter.init();

    let seed = RandNumGen.genseed();
    let RNG = RandNumGen.new(seed);

    //do Tile.drawGrid();
    //do Main.drawFirstRow();
    //do Main.drawAlphabet();

    do Main.game();

    return;
  }

  function void game() {
    var String guess;
    var int round, n, j, k;
    var boolean won;

    let n = RNG.rand();
    let secret = Dictionary.word(Util.mod(n, Dictionary.size()));
    //let secret = "STUFF";

    //do Main.printSecret(secret); //for debugging

    let j = 0;
    while (j < 6) {
      let k = 0;
      while (k < 5) {
        do Tile.drawDottedTile(j + 1, (k + 5));
        let k = k + 1;
      }
      let j = j + 1;
    }

    let round = 0;
    while(~won & (round < 6)) {
        let guess = Main.readWord(round + 1);
        let won = Main.checkGuess(round + 1, guess);
        let round = round + 1;
    }

    if(won) {
        do Output.moveCursor(1, 27);
        do Output.printString("You Won!");
    } else {
        do Output.moveCursor(1, 28);
        do Output.printString(secret);
    }

    return;
  }

  function void printSecret(String secret) {
    do Output.moveCursor(22,0);
    do Output.printString(secret);
    do Output.moveCursor(0,0);
    return;
  }


  function String readWord(int row) {
    var int i;
    var char c;
    var String word;
    let word = String.new(5);

    let i = 0;
    while (i < 5) {
      let c = 0; // c is invalid at the start.
      while (~Main.isLetter(c)) {
        let c = Keyboard.readChar();
        do Output.backSpace();
      }
      do word.appendChar(c);
      do Tile.drawLetter(c, row, 5+i);
      let i = i + 1;
    }
    return word;
  }

  //returns true if correct guess
  //sideeffect: draw to screen
  function boolean checkGuess(int row, String guess) {
    var int i, k, result, c;
    var boolean isexact;
    var Array outstanding;
   
    //initialize outstanding to 0s
    let outstanding = Array.new(27);
    let i = 0;
    while(i < 27) {
        let outstanding[i] = 0;
        let i = i + 1;
    }

    //count letters in secret
    let i = 0;
    while(i < 5) {
        let k = secret.charAt(i)-65;
        let outstanding[k] = outstanding[k] + 1;
        let i = i + 1;
    }

    let isexact = true;
   
    //check for exacts
    let i = 0;
    while (i < 5) {
        let result = Main.checkChar(i, guess, outstanding);
        if(result = GUESS_EXACT) {
          let c = guess.charAt(i);
          do Tile.drawSolidLetter(c, row, 5+i);
          let k = c - 65;
          let outstanding[k] = outstanding[k] - 1;
        } else {
          let isexact = false;
        }
        let i = i + 1;
    }

    //if(isexact) {
    //    return true;
    //}

    //check the rest
    let i = 0;
    while (i < 5) {
        let result = Main.checkChar(i, guess, outstanding);
        if (result = GUESS_IN_WORD) {
          do Tile.drawGrayLetter(guess.charAt(i), row, 5+i);
        } else{if(result = GUESS_WRONG) {
          do Tile.drawOutlinedLetter(guess.charAt(i), row, 5+i);
        }}
        let i = i + 1;
    }

    do outstanding.dispose();

    return isexact;
  }

  function int checkChar(int i, String guess, Array outstanding) {
    var char c;
    var int j;

    let c = guess.charAt(i);
    if (c = secret.charAt(i)) {
      return GUESS_EXACT;
    }

    if(~(outstanding[c-65] = 0)) {
        let j = 0;
        while (j < 5) {
          if (c = secret.charAt(j)) {
            return GUESS_IN_WORD;
          }
          let j = j + 1;
        }
    }

    return GUESS_WRONG;
  }

  function boolean isLetter(char c) {
    return ((c > 64) & (c < 91)) | (c = 59);
  }

  function void drawFirstRow() {
    do Tile.drawSolidLetter(83, 0, 5);
    do Tile.drawOutlinedLetter(72, 0, 6);
    do Tile.drawOutlinedLetter(73, 0, 7);
    do Tile.drawSolidLetter(82, 0, 8);
    do Tile.drawGrayLetter(69, 0, 9);
    
    return;
  }

  function void drawAlphabet() {
    var int i;
    let i = 0;
    do Screen.setColor(true);
    do Screen.drawRectangle(0, 224, 511, 255);
    while (i < 27) {
      do Letter.draw(i + 65, i + 6400 + 2, true);
      do Letter.draw(i + 65, i + 7424 + 2, false);
      let i = i + 1;
    }
    return;
  }
}
